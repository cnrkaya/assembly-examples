;QUICK SORT YÖNTEMIYLE, KULLANICIDAN ALINAN DIZIYI 
;SIRALAYAN EXE TIPINDE PROGRAM.
myds SEGMENT PARA 'Data'  
     CR EQU 13   
     LF EQU 10
     PIVOT DW ?
     BAS DW ?
     SON DW ?
	 sizeD dW ?
     dizi DB 100 DUP(?)
	 myinfo db CR,'CANER KAYA 16011087',0
     msg_size db LF,CR,'Dizinin boyutunu giriniz: ',0
     msg_input db LF,CR, 'Dizinin Elemanlarini Giriniz',LF,0 
	 msg_ilk_durum db LF,CR,'Ilk Durumda Dizi : ',0 
	 msg_sirali db LF,CR,'Sirali dizi : ',0
     msg_hata DB LF,CR,'DIKKAT! HATALI SAYI VERDIN ',LF,0    
myds ends 

myss SEGMENT PARA STACK 'STACK'
     DW 20 DUP(?)
myss ends

mycs SEGMENT PARA 'CODE'
     ASSUME DS:myds, CS:mycs, SS:myss
     
ANA  PROC FAR
         
     PUSH DS
     XOR AX,AX
     PUSH AX
     ;DATASEGMENTE ERISEBILMEK ICIN
     MOV AX,myds
     MOV DS,AX
      
     ;OGRENCI BILGILERI YAZDIRILIR
	 LEA AX,myinfo
     CALL PUT_STR 
     
	;DIZI BOYUTU ALINIR UYGUN ARALIKTA DEGILSE TEKRAR ALINIR
 W:  LEA AX,MSG_SIZE      
     CALL PUT_STR       
     CALL getn;GETN DEN DONEN DEGER AX REGISTERINDA
     CMP AX,100 ;100 DEN BUYUKSE TEKRAR AL
	 JA W
	 CMP AX,0  ;0DAN KUCUK ESITSE TEKRAR AL
	 JBE W
	 MOV sizeD,AX ;DIZI BOYUTU ATANDI
     
	 ;KULLANICIDAN DIZI ELEMANLARI ALINIYOR
     LEA ax,msg_input      
     CALL PUT_STR
     CALL GETARR      
	 
	 ;DIZI EKRANA YAZDIRILIYOR
	 LEA AX,msg_ilk_durum
	 CALL PUT_STR
     LEA BX,DIZI  
     CALL PUTN 
     
     ;SIRALAMA PROCEDURU CAGRILIYOR
     MOV AX,sizeD  
     DEC AX
     MOV SON,AX  ;DIZININ SONU=BOYUT-1
     MOV BAS,0    ;DIZININ BASI=0
     CALL SORT  
     
	;SIRALI DIZI EKRANA YAZDIRILIYOR
     LEA AX,msg_sirali
	 CALL PUT_STR
     LEA BX,DIZI  
     CALL PUTN      
        
    RETF
ANA ENDP  

SORT PROC NEAR 
	;Quick sort yontemiyle RECURSIVE SEKILDE diziyi sIRALAYAN PROCEDURE.
	;DIZININ BAS INDISINDEN SON INDISINE KADAR OLAN BOLUMU ELE ALINIR
	;ORTADAKI ELEMAN PIVOT OLARAK SECILIR ,PIVOTTAN BUYUK ELEMANLAR DIZININ SAGINA
	;KUCUK ELEMANLAR DIZININ SOLUNA YERLESTIRILDIKTEN SONRA PIVOTUN SOLUNDA KALAN 
	;DIZI PARCASI VE SAGINDA KALAN DIZI PARCASI ICIN YENI BAS VE SON INDISLERIYLE
	;FONKSIYON TEKRAR CAGRILIR

	;FONKSIYON CAGRILARINDAN DONULDUGUNDE ONCEKI DEGERLERIN KORUNMASI ICIN
	;BAS ve SON PUSHLANARAK local degisken haline getirildi
     PUSH BAS  
     PUSH SON
     
	 ;DIZININ BAS INDISI SON INDISINDEN BUYUK ESITSE
     ;DIZI SIRALANMISTIR PROCEDURDEN CIK	 
     MOV AX,BAS
     CMP AX,SON               
     JNL finish1  ;ISARETLI DALLANMA KULLANILDI CUNKU SON INDISI AZALTILDIGI ICIN
	              ;BAZI DURUMLARDA(PIVOT DIZININ EN KUCUK ELEMANIYSA)-1 DEGERI ALABILIYOR
				  ;BU YÜZDEN GARANTIYE ALMAK ICIN TUM KARSILASTIRMALAR ISARETLI YAPILDI

	;DIZININ ORTASINDAKI ELEMAN PIVOT SECILDI  
     MOV AX,SON
     ADD AX,BAS
     SHR AX,1  ;AX=AX/2
     MOV PIVOT,AX  ;PIVOT=(BAS+SON)/2 
     
	 
	 ;PIVOT ILK ELEMANLA YER DEGISTIRILDI
     MOV SI,BAS
     MOV DI,PIVOT
     MOV AL,DIZI[SI] 
     XCHG AL,DIZI[DI] ;ILK ELEMAN PIVOTUN YERINE GECTI
     MOV DIZI[SI],AL  ;PIVOT DIZININ BASINA GELDI
	 
	 ;SI DIZININ BASINDAN SONUNA ILERLEYECEK
	 ;DI DIZININ SONUNDAN BASINA ILERLEYECEK
     MOV BX,SI ;DIZI[BX] PIVOTU GOSTERIYOR
     INC SI    ;PIVOTTAN SONRAKI ILK ELEMAN
     MOV DI,SON

DONGU:     
     ;PIVOTTAN BUYUK DEGERLER SAGA KUCUK DEGERLER SOLA
	 ;OLACAK SEKILDE DIZI IKI PARCAYA BOLUNUYOR
    
   	 CMP SI,DI
     JNLE DIVIDED ;DIZI BOLUNMUSTUR ;
	
	  
	  
    JMP gec        ;IF KOLU CIKIS NOKTASINA UZAK OLDUGU ICIN
finish1:JMP finish   ;ARA ZIPLAMA ISLEMI YAPILDI 
gec:
		
IC_DONGU_1:     
     ;SI SAYACI SONA GELENE VEYA 
     ;PIVOTTAN BUYUK ELEMAN BULANA KADAR DON 
     CMP SI,SON
     JNLE EXT  ;DIZININ SONUNA GELDIYSE CIK
     MOV AL,DIZI[SI]
     CMP AL,DIZI[BX] ;BX PIVOTU TUTUYORDU
     JNLE IC_DONGU_2 ;PIVOTTAN BUYUK DEGER BULUNDUYSA DONGUDEN CIK
     INC SI          ;SONRAKI ELEMANA BAK
     JMP IC_DONGU_1            
IC_DONGU_2:
	 ;DI SAYACI BASA GELENE VEYA 
     ;PIVOTTAN KUCUK ELEMAN BULANA KADAR DON 
     CMP DI,BAS
     JNGE EXT  ;DIZININ BASINA GELDIYSE CIK
     MOV AL,DIZI[DI] ;DI SAYACININ GOSTERDIGI ELEMAN AL DE
     CMP AL,DIZI[BX] ;PIVOT ILE ELEMANI KARSILASTIR
     JNG EXT         ;PIVOTTAN KUCUK DEGER BULUNDUYSA DONGUDEN CIK
     DEC DI          ;SONRAKI ELEMANA BAK
     JMP IC_DONGU_2
EXT:         
     CMP SI,DI ;ZIT YONDE ILERLEYEN SAYACLAR KARSILASTIRILIYOR
     JNL FALSE ;SI DI DAN BUYUKSE ELEMANLAR DOGRU YERDE
     MOV AL,DIZI[SI]  ;PIVOTUN SOLUNDA BULUNAN BUYUK DEGERLE
     XCHG AL,DIZI[DI] ;PIVOTUN SAGINDA BULUNAN KUCUK DEGER YER DEGISTIRILIYOR   
     MOV DIZI[SI],AL
FALSE:   
     JMP DONGU 
DIVIDED:           
     MOV SI,BAS   ;SI DIZININ ILK ELEMANINI YANI PIVOTU GOSTERIYOR    
     MOV AL,DIZI[SI]
     XCHG AL,DIZI[DI] ;DI PIVOTTAN KUCUK KISMIN EN SAGINDAKI ELEMANINI GOSTERIYOR
     MOV DIZI[SI],AL  ;PIVOT KENDINDEN BUYUK VE KUCUKLERIN ARASINA GETIRILMIS OLDU
             
     ;PROCEDUR PIVOTUN SOLUNDA KALAN PARCA ICIN TEKRAR CAGRILACAK    
     PUSH SON ;ASAGIDA 2. CAGRIDA AYNI SON DEGERI KULLANILACAGI ICIN SAKLANMALI
     DEC DI   ;SUAN DI PIVOTUN SOLUNU GOSTERIYOR  
     MOV SON,DI;PIVOTUN SOLUNDAKI ELEMAN DIZININ SONU OLDU
			   ;BAS DEGERI 0 GUNCELLEMEYE GEREK YOK
     CALL SORT ;PIVOTUN SOLUNDA KALAN PARCA ICIN CAGRILDI
     POP SON  
     
	 ;PROCEDUR PIVOTUN SAGINDA KALAN PARCA ICIN TEKRAR CAGRILACAK
     INC DI    ;DI EN SON PIVOTUN SOLUNU GOSTERIYORDU
     INC DI    ;ARTIK PIVOTUN SAGINI GOSTERIYOR
     MOV BAS,DI;PIVOTUN SAGINDAKI ELEMAN DIZININ BAS ELEMANI OLDU
				;SON DEGERI KORUNMUSTU DIZININ SONUNU GOSTERIYOR
     CALL SORT  ;PIVOTUN SAGINDA KALAN PARCA ICIN CAGRILDI
     DEC DI  ;DI TEKRARDAN PIVOTU GOSTERIR HALE GETIRILDI
     
finish:
     ;FONKSIYONLAR GERI DONERKEN PARAMETRELER CAGRILDIKLARI DEGERLERINI KORUYOR
     POP SON 
     POP BAS               
     RET
SORT ENDP    
     
GETARR PROC NEAR
    ;KULLANICIDAN DIZININ ELEMANLARINI ALAN PROCEDURE
	
	;CX ,SI STACKTE SAKLANIYOR
	PUSH CX
	PUSH SI
	
    MOV CX,sizeD ;CX DIZI BOYUTU KADAR LOOPU DÖNDÜRÜR
    XOR SI,SI    ;DIZININ 0. ELEMANINDAN ITIBAREN ERISMEK ICIN 
  L1:CALL GETN  ;KULLANICININ GIRDIGI DEGERI AL YE YAZAR
    MOV dizi[SI],AL ;GIRILEN DEGER DIZIYE YE YAZDIRILIR
    INC SI       ;DIZININ SONRAKI INDISINE GECILIR
    LOOP L1      
	POP SI
	POP CX
	RET
GETARR ENDP       
     

PUTC PROC NEAR
     ;al registerindaki KARAKTERI ekrana yazdirir
     
	 ;REGISTER DEGERLERI SAKLANIR
     PUSH AX
     PUSH DX  
     
     MOV DL,AL 
     MOV AH,2 
     INT 21H ;AH DA 2 DEGERI OLDUGU 
	 ;ICIN STANDART OUTPUTA KARAKTER YAZDIRILIR
    
     POP DX
     POP AX  
     RET    
PUTC ENDP

PUT_STR PROC NEAR
        ;ax de adresi bulunan stringi yazdirir
        
        PUSH BX    ;bx degeri korunur
        MOV BX,AX
        MOV AL , BYTE PTR[BX] ;HER CHAR 1 BYTE OLDUGU ICIN
        
PUT_L:
        CMP AL,0 ;STRING IN SONU MU DIYE KONTROL YAPILIYOR
        JE PUT_SON
        CALL PUTC ;KARAKTER YAZDIRAN PROCEDURE CAGRILIR
        INC BX    ;SONRAKI KARAKTERE GECILIR
        MOV AL,BYTE PTR[BX]
        JMP PUT_L 

PUT_SON:
        POP BX
        RET
PUT_STR ENDP          

GETC PROC NEAR
;klavyeden basilan karakteri al registerina alir ve ekranda gosterir
;islem sonucunda al etkilenir

     MOV AH,1H
     INT 21H ;AH<-1 NUMARALI INTERRUPT STD.INPUTTAN KARAKTER OKUR
     RET
GETC ENDP

  

GETN PROC NEAR
;KULLANICININ GIRDIGI SAYIYI OKUYUP AX YAZMACI ÜZERİNDEN DÖNDÜRÜR
;DX: SAYININ ISARET BILGISINI TUTAR 1 ISE SAYI POZITIF ,-1 ISE NEGATIF
;BL HANE BILGISINI TUTAR
;CX: OKUNAN SAYI ISLENIRKEN ARA DEGERI TUTAR
;AL :ICERISINDE OKUNAN KARAKTERIN ASCII KARSILIGI BULUNUR
     PUSH BX
     PUSH CX
     PUSH DX
     
STRT:
     MOV DX,1 ;BASLANGICTA SAYI POZITIF KABUL EDILIR
     XOR BX,BX ;HANE SIFIRLANDI
     XOR CX,CX ;ARA DEGER SIFIRLANDI
     
NEW: 
    CALL GETC  ;KULLANICIN GIRDIGI DEGERI AL YE YAZAR
    CMP AL,13  ;AL ENTER IN ASCII KARSILIGI ISE OKUMA BITER
    JE FIND_READ
    CMP AL,'-'  ;AL '-' ISE NEGATIVE ISLEMLERI UYGULANIR
    JNE CTRL_NUM
    
NEGATIVE:
    MOV DX,-1  ;SAYI ISARETI NEGATIF OLDUGU BELIRLENDI
    JMP NEW    ;YENI HANE ALMAK UZERE DONGUYE GIRER

CTRL_NUM:
    CMP AL,'0' ;SAYI 0 DAN KUCUK MU
    JB ERROR
    CMP AL,'9' ;SAYI 9 DAN BUYUK MU
    JA ERROR
    SUB AL,'0' ;RAKAMIN INTEGER KARSILIGI AL DE
    MOV BL,AL  ;DEGER BL DE
    MOV AX,10  ;HANE 10 ILE CARPILACAK
    PUSH DX    ; CARPMA SONUCU DX BOZULUR
    MUL CX     ;DX:AX=AX*CX
    POP DX     ;ISARET BILGISI GERI ALINDI
    MOV CX,AX  ;CX DEKI ARA DEGER *10 OLDU
    ADD CX,BX  ;OKUNAN HANE EKLENDI
    JMP NEW ;YENI HANE ALMAK UZERE DONGUYE GIRER
    
ERROR:
    MOV AX,OFFSET msg_hata ;HATA MESAJI YAZDIRILIR
    CALL PUT_STR           
    JMP STRT			   ;YENIDEN SAYI ALMAYA BASLA
FIND_READ:  

	
	CMP CX,-128  ;SAYI -128 DEN KUCUK MU
    JL ERROR
    CMP CX,127   ;SAYI 127DEN BUYUK MU
    JG ERROR
    
    MOV AX,CX    ;SAYI CX DEYDI
    CMP DX,1     ;SAYI POZITIF MI
    JE FIND_GETN  
    NEG AX       ;SAYININ ISARETI EKLENIR 
FIND_GETN:
    POP DX
    POP CX
    POP BX
    RET
GETN ENDP 

PUTN PROC NEAR
;BX de adresi bulunan sayi dizisini onluk tabanda hane hane yazdirir
;cx :haneleri 10 a bolerek bulacagiz cx=10 olacak
;dx 32 bolmede isleme dahil olacak sonucu etkilemesin diye 0 olmali
    PUSH CX
    PUSH DX
    MOV CX,sizeD ;DIZININ BOYUTU CX E ATANIR
    
L:  PUSH CX ;CX ARA ISLEMLERDE KULLANILDIGI ICIN SAKLANDI
    MOV AL,BYTE PTR[BX] ;DIZININ ELEMANI AL YE ATANIR
    CBW  ;SIGN BITI KAYBETMEMEK ICIN ONEMLI
    XOR DX,DX ;32 BIT BOLMEDE SONUCU ETKILEMESIN DIYE SIFIRLANDI
    PUSH DX  ;HANELERI ASCII KARAKTER OLARAK SAKLAYACAGIZ 
			;KAC HANEYI ALACAGIMIZI BILMEDIGIMIZ ICIN STACKTEN 
			;0 DEGERININ CEKENE KADAR DEVAM EDILECEK
    
    MOV CX,10
    CMP AX,0   ;SAYI NEGATIF MI
    JGE CALC_DIGITS
    NEG AX     ;SAYI POZITIF YAPILDI
    PUSH AX    ;SAYI SAKLANIR
    MOV AL, '-';EKRANA EKSI ISARETI YAZILIR 
    CALL PUTC
    POP AX      ;SAYI CEKILDI
CALC_DIGITS:
    DIV CX      ;SAYI 10A BOLUNUR, DX ZATEN SIFIRDI
    ADD DX,'0'  ;KALANIN ASCII DEGERI HESAPLANIR
    PUSH DX     ;DX SAKLANIR
    XOR DX,DX   
    CMP AX,0    ;BOLUM 0 SA ISLEM BITT
    JNE CALC_DIGITS
DISP_LOOP: 
	;USTTE EN ANLAMLI BIT OLACAK SEKILDE TUM HANELER YIGINDA 
    POP AX
    CMP AX,0  ;SAYININ SONUNA GELDIGIMIZI GORMEK ICIN YIGINA 0 ATMISTIK
    JE END_DISP_LOOP
    CALL PUTC ;AL DEKI KARAKTERI YAZ
    JMP DISP_LOOP
END_DISP_LOOP:
    MOV AL,' ' ;YENI SAYI ILE ARASINA BOSLUK KOYULUR
    CALL PUTC
    POP CX
    INC BX  ;DIZININ SONRAKI ELEMANINA GECILIR
    LOOP L
   
    POP DX
    POP CX
    RET
PUTN ENDP


mycs ends
     end ANA
 